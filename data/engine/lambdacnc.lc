identity = M44F r1 r2 r3 r4
  where
    r1 = V4 1 0 0 0
    r2 = V4 0 1 0 0
    r3 = V4 0 0 1 0
    r4 = V4 0 0 0 1

quad size = fetchArrays (quadPosition, quadUV)
  where
    s = size - 1

    quadPosition =
      [ V2 (-1) (-1), V2 s s, V2 (-1) s
      , V2 (-1) (-1), V2 s (-1), V2 s s
      ]

    quadUV =
      [ V2 0 0, V2 1 1, V2 0 1
      , V2 0 0, V2 1 0, V2 1 1
      ]

----------------------------------------

aspectRatio screenSize = screenSize%x / screenSize%y

nearPlane = 30000
farPlane = 300000

linearizeDepth depth =
    let
        z = depth * 2.0 - 1.0 -- Back to NDC 
        n = nearPlane
        f = farPlane
    in
    -- (2.0 * n * f) / (f + n - z * (f - n))
    depth / (f/7 - n)
    
lightPos time = rotMatrixZ (time*8) *. V4 (80000) (10000) (40000) 1
cameraPos time = V4 0 180000 60000 0

cameraMat screenSize time = projection .*. view
  where
    projection = perspective nearPlane farPlane (30 * pi / 180) (aspectRatio screenSize)
    view = lookat (cameraPos time)%xyz (V3 0 0 10000) (V3 0 0 1)

orthographic :: Float
             -> Float
             -> Float
             -> Float
             -> Mat 4 4 Float
orthographic n f t aspect =
    M44F (V4 (2/(r-l))      0              0              0)
         (V4 0              (2/(t-b))      0              0)
         (V4 0              0              (-2/(f-n))     0)
         (V4 (-(r+l)/(r-l)) (-(t+b)/(t-b)) (-(f+n)/(f-n)) 1)
  where
    b = -t
    r = aspect*t
    l = -r

lightMat depthMapSize time = projection .*. view
  where
    projection = orthographic nearPlane farPlane 50000 (aspectRatio depthMapSize)
    -- projection = perspective nearPlane farPlane 45 (depthMapSize%x / depthMapSize%y)
    view = lookat (lightPos time)%xyz (V3 0 0 0) (V3 0 0 1)

----------------------------------------

modelMat time =
    -- identity
    -- rotMatrixZ (time * 4)
    rotMatrixZ (pi/4*3)


positionObject time origPos vertPos =
    V4 vertPos%x vertPos%y vertPos%z 1 + V4 origPos%x origPos%y origPos%z 0


vertObject depthMapSize time screenSize origPos vertPos n =
    (pos, fragPos, fragPosLightSpace, uvw, normal)
  where
    model = modelMat time

    objPos = positionObject time origPos vertPos

    pos = cameraMat screenSize time *. model *. objPos
    fragPos = model *. objPos
    fragPosLightSpace = lightMat depthMapSize time *. fragPos
    uvw = vertPos + V3 24000 0 0
    normal = normalize $ model *. (V4 n%x n%y n%z 0)


vertShadow depthMapSize time origPos vertPos =
    (pos, pos%z * 0.5 + 0.5)
  where
    model = modelMat time

    objPos = positionObject time origPos vertPos

    pos = lightMat depthMapSize time *. model *. objPos

----------------------------------------

diffuseLight fp normal lightPos lightColor =
    let
        lightDir = normalize (lightPos - fp)
        diff = max (dot normal lightDir) 0
        diffuse = lightColor *! diff
    in
    diffuse

fragObject time texture depth fp lsfp uv normal =
    V4 result%x result%y result%z 1
  where
    textureMap = texture2D (Sampler LinearFilter ClampToEdge texture)
    depthMap = texture2D (Sampler LinearFilter ClampToEdge depth)

    lightDir = normalize (lightPos time - fp)

    diffuse = diffuseLight fp normal (lightPos time) (V3 1.0 1.0 1.0)
            -- + diffuseLight fp n (V4 0 0 20000 1) (V3 1.0 1.0 1.0) *! 0.5
            -- + diffuseLight fp n (V4 0 50000 0 1) (V3 1.0 1.0 1.0) *! 0.5
            -- + diffuseLight fp n (V4 50000 0 0 1) (V3 1.0 1.0 1.0) *! 0.5
    objectColor = textureMap (uv%xy *! 0.000025)

    -- perform perspective divide
    projCoords = (lsfp%xyz /! lsfp%w) *! 0.5 +! 0.5
    -- get closest depth value from light's perspective (using [0,1] range fragPosLight as coords)
    closestDepth = (depthMap projCoords%xy)%r
    -- get depth of current fragment from light's perspective
    currentDepth = projCoords%z
    -- check whether current frag pos is in shadow
    bias = max (0.05 * (1.0 - (dot normal lightDir))) 0.005
    shadow = if currentDepth - bias > closestDepth then 0.3 else 1.0

    result = diffuse * objectColor%xyz *! shadow

----------------------------------------

-- Roughly put the light emitter in the center of the bulb by moving the bulb's
-- sphere up (in Z direction).
bulbOffset = V4 0 0 4200 0

vertBulb screenSize time vertPos n =
    let
        objPos = rotMatrixX (-pi/2) *. scale 200 (V4 vertPos%x vertPos%y vertPos%z 0) + lightPos time + bulbOffset
        pos = cameraMat screenSize time *. objPos
    in
    (pos, n)

----------------------------------------

makeFrame (depthMapSize :: Vec 2 Float)
          (screenSize :: Vec 2 Float)
          (time :: Float)
          (position :: Vec 3 Float)
          (texture :: Texture)
          (depth :: Texture)
          (objects :: PrimitiveStream Triangle (Vec 3 Float, Vec 3 Float))
          (lights :: PrimitiveStream Triangle (Vec 3 Float, Vec 3 Float))
    = imageFrame (emptyDepthImage 1, emptyColorImage (V4 0.7 0.7 0.7 1))
  `overlay` bulbsWireFrame
  `overlay` bulbsSurfaces
  `overlay` objectsWireFrame
  `overlay` objectsSurfaces
  `overlay` showTextureOn (quad 0.5) depth
  where
    bulbsWireFrame = lights
      & mapPrimitives (\((pos,n)) -> vertBulb screenSize time pos n)
      & rasterizePrimitives (TriangleCtx CullNone (PolygonLine 0) NoOffset LastVertex) ((Smooth))
      & mapFragments (\((_)) -> ((black)))
      & accumulateWith (DepthOp Less True, ColorOp NoBlending (V4 True True True True))

    bulbsSurfaces = lights
      & mapPrimitives (\((pos,n)) -> vertBulb screenSize time pos n)
      & rasterizePrimitives (TriangleCtx CullNone PolygonFill NoOffset LastVertex) ((Smooth))
      & mapFragments (\((_)) -> ((white)))
      & accumulateWith (DepthOp Less True, ColorOp NoBlending (V4 True True True True))

    objectsWireFrame = objects
      & mapPrimitives (\((pos,n)) -> vertObject depthMapSize time screenSize position pos n)
      & rasterizePrimitives (TriangleCtx CullNone (PolygonLine 0) NoOffset LastVertex) (Smooth, Smooth, Smooth, Smooth)
      & mapFragments (\((_,_,_,_)) -> ((V4 0 0 0 0.5)))
      & accumulateWith (DepthOp Less True, ColorOp NoBlending (V4 True True True True))

    objectsSurfaces = objects
      & mapPrimitives (\((pos,n)) -> vertObject depthMapSize time screenSize position pos n)
      & rasterizePrimitives (TriangleCtx CullNone PolygonFill NoOffset LastVertex) (Smooth, Smooth, Smooth, Smooth)
      & mapFragments (\((fp,lsfp,uv,n)) -> ((fragObject time texture depth fp lsfp uv n)))
      & accumulateWith (DepthOp Less True, ColorOp NoBlending (V4 True True True True))


makeShadowMap (depthMapSize :: Vec 2 Float)
              (time :: Float)
              (position :: Vec 3 Float)
              (objects :: PrimitiveStream Triangle (Vec 3 Float, Vec 3 Float))
    = imageFrame (emptyDepthImage 1, emptyColorImage (V4 1 1 0 1))
  `overlay` objectsSurfaces
  where
    objectsSurfaces = objects
      & mapPrimitives (\((pos,_)) -> vertShadow depthMapSize time position pos)
      & rasterizePrimitives (TriangleCtx CullFront PolygonFill NoOffset LastVertex) ((Smooth))
      & mapFragments (\((z)) -> fragColor z)
      & accumulateWith (DepthOp Less True, ColorOp NoBlending (V4 True True True True))

    fragColor z = 
      -- let c = linearizeDepth z in
      let c = z in
      ((V4 c c c 1))


showTextureOn (prims :: PrimitiveStream Triangle (Vec 2 Float, Vec 2 Float))
              (texture :: Texture)
    = prims
    & mapPrimitives (\(p, uv) -> (V4 p%x p%y (-1) 1, uv))
    & rasterizePrimitives (TriangleCtx CullNone PolygonFill NoOffset LastVertex) ((Smooth))
    & mapFragments (\((uv)) -> ((texture2D (Sampler PointFilter Repeat texture) uv)))
    & accumulateWith (DepthOp Less True, ColorOp NoBlending (V4 True True True True)) 


showTexture texture = imageFrame (emptyDepthImage 1, emptyColorImage (V4 0 0 0.4 1))
  `overlay`
    showTextureOn (quad 2) texture

main = renderFrame
    frame
    -- (showTexture shadowMapTexture)
    -- shadowMap
  where
    frame = makeFrame
        depthMapSize
        (Uniform "screenSize")
        (Uniform "time" / 10)
        (Uniform "position")
        (Texture2DSlot "diffuseTexture")
        shadowMapTexture
        (fetch "objects" (Attribute "position", Attribute "normal"))
        (fetch "lights" (Attribute "position", Attribute "normal"))

    shadowMapTexture = Texture2D depthMapSize (PrjImageColor shadowMap)
    shadowMap = makeShadowMap
        depthMapSize
        (Uniform "time" / 10)
        (Uniform "position")
        (fetch "objects" (Attribute "position", Attribute "normal"))

    depthMapSize = V2 960 540
